# Auto compile
if [[ ~/.zshrc -nt ~/.zshrc.zwc ]]; then
  zcompile ~/.zshrc
fi

# Load kube-ps1 if available
local kube_ps1_paths=(
  /opt/homebrew/opt/kube-ps1/share/kube-ps1.sh    # Apple Silicon
  /usr/local/opt/kube-ps1/share/kube-ps1.sh       # Intel
)
for kube_ps1_path in "${kube_ps1_paths[@]}"; do
  if [[ -f "$kube_ps1_path" ]]; then
    source "$kube_ps1_path"
    break
  fi
done

# Zsh plugin conf
MNML_PROMPT=(mnml_ssh 'mnml_cwd 0 0' mnml_status mnml_keymap kube_ps1 mnml_git)
MNML_RPROMPT=('')
MNML_USER_CHAR='#'
MNML_INSERT_CHAR='>'

# Load sheldon plugins
eval "$(sheldon source)"

# Export brew bin path
if [[ "$(uname -m)" == "arm64" ]]; then
  export PATH="/opt/homebrew/bin:$PATH"
  export PATH="/opt/homebrew/sbin:$PATH"
else
  export PATH="/usr/local/bin:$PATH"
  export PATH="/usr/local/sbin:$PATH"
fi

# Notify (disabled by default, enable with ZSHRC_NOTIFY=1)
notify_precmd() {
  if [[ "$ZSHRC_NOTIFY" != "1" ]]; then
    return
  fi

  local prev_command_status=$?

  if [[ "${TTYIDLE:-0}" -gt 1 ]]; then
    local notify_title
    if [[ "$prev_command_status" -eq 0 ]]; then
      notify_title="Command succeeded"
    else
      notify_title="Command failed"
    fi
    # Run in background to avoid blocking shell
    osascript -e "display notification \"$prev_command\" with title \"$notify_title\"" &!
  fi
}

store_command() {
  prev_command=$2
}

autoload -Uz add-zsh-hook
add-zsh-hook preexec store_command
add-zsh-hook precmd notify_precmd

# Change colors
autoload -Uz colors

# Configure for history
HISTFILE=~/.zsh_history
HISTSIZE=1000000
SAVEHIST=1000000
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_FIND_NO_DUPS
setopt HIST_REDUCE_BLANKS
setopt HIST_NO_STORE
setopt SHARE_HISTORY

export EDITOR=nvim

# Specify word delimiter
autoload -Uz select-word-style
select-word-style default

# Match lowercase letters or capital letters in completion
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# After ../ do not complement the directory that is now
zstyle ':completion:*' ignore-parents parent pwd ..

# Process name completion of ps command
zstyle ':completion:*:processes' command 'ps x -o pid,s,args'

# Options
# Make Japanese filename displayable
setopt print_eight_bit

# Disable beep
setopt no_beep

# Disable flow control
setopt no_flow_control

# Do not quit zsh with Ctrl + D
setopt ignore_eof

# Treat after '#' as comment
setopt interactive_comments

# Cd by directory name alone
setopt auto_cd

# Automatically pushd after cd
setopt auto_pushd

# Do not add duplicate directory name
setopt pushd_ignore_dups

# Use advanced wildcard deployment
setopt extended_glob

# Make wildcard available with * when ^ R search history search
bindkey '^R' history-incremental-pattern-search-backward

# Control type of ctrl
bindkey -e

# Query overwrite file
# @system
alias cp='cp -i'
# @system
alias mv='mv -i'
# @system
alias mkdir='mkdir -p'

# For eza alias & shortcut
# @system
alias ls='eza -GF'
# @system
alias ll='eza -l'
# @system
alias la='eza -alh'

# For nvim
# @system
alias vi='nvim'

# For git
# @git
alias g='git'

# For brew
# @brew
alias bu='env HOMEBREW_INSTALL_CLEANUP=1 brew upgrade --fetch-HEAD --display-times && brew upgrade --cask'

# For Docker
# @docker
alias de='docker exec'
# @docker
alias d-c='docker-compose'

# For kubectl
# @kubectl
alias k='kubectl'
# @kubectl
alias kg='kubectl get'
# @kubectl
alias kc='kubectx'
# @kubectl
alias kn='kubens'
# @kubectl
alias kv='k9s'

# For reset
# @system
alias re='exec $SHELL -l'

# for fzf
fzf-z-search() {
  local res=$(z | sort -rn | cut -c 12- | fzf)
  if [[ -n "$res" ]]; then
    BUFFER+="cd $res"
    zle accept-line
  else
    return 1
  fi
}
zle -N fzf-z-search
bindkey '^v' fzf-z-search

fzf-src() {
  local selected_dir=$(ghq list -p | fzf --query "$LBUFFER")
  if [[ -n "$selected_dir" ]]; then
    BUFFER="cd ${selected_dir}"
    zle accept-line
  fi
}
zle -N fzf-src
bindkey '^j' fzf-src

fzf-history() {
  BUFFER=$(fc -l -r 1 | awk '{$1=""; print substr($0,2)}' | uniq | fzf)
  CURSOR=$#BUFFER
}
zle -N fzf-history
bindkey '^]' fzf-history

git-branch-fzf() {
  local selected_branch=$(git for-each-ref --format='%(refname)' --sort=-committerdate refs/heads | perl -pne 's{^refs/heads/}{}' | fzf --query "$LBUFFER")

  if [[ -n "$selected_branch" ]]; then
    BUFFER="git checkout ${selected_branch}"
    zle accept-line
  fi

  zle reset-prompt
}

zle -N git-branch-fzf
bindkey "^[" git-branch-fzf

tree-fzf() {
  local SELECTED_FILE=$(tree --charset=o -f | fzf --query "$LBUFFER" | tr -d '\||`|-' | xargs echo)

  if [[ -n "$SELECTED_FILE" ]]; then
    BUFFER="$EDITOR $SELECTED_FILE"
    zle accept-line
  fi

  zle reset-prompt
}

zle -N tree-fzf
bindkey "^t" tree-fzf

# For ssh
set_term_bgcolor() {
  local R=${1}*65535/255
  local G=${2}*65535/255
  local B=${3}*65535/255

  /usr/bin/osascript <<EOF
  tell application "iTerm2"
    tell current session of current window
      set background color to {$R, $G, $B}
    end tell
  end tell
EOF
}

myssh() {
  set_term_bgcolor 0 0 30
  \ssh "$@"
  set_term_bgcolor 0 0 0
}

# @system
alias ssh='myssh'

# For vscode
# @system
alias e='code -r'

# For gnu-sed
if [[ "$(uname)" == "Darwin" ]]; then
  # @system
  alias sed='gsed'
fi

# Alias management function
aliases() {
  local file="$HOME/.zshrc"
  if [[ -z "${1:-}" ]]; then
    # Display all aliases with category
    grep -B 1 "^alias [a-z]" "$file" | grep -v "^--$" | awk '/^# @/{tag=$0; next} {print tag " " $0}' | sed 's/# @//' | column -t
  else
    # Display aliases for specific category
    grep -B 1 "^alias [a-z]" "$file" | grep -v "^--$" | awk "/^# @$1/{tag=1; next} /^# @/{tag=0} tag && /^alias/{print}" | column -t
  fi
}

# For direnv
eval "$(direnv hook zsh)"

# For mise (lazy load)
eval "$(mise activate zsh --shims)"

# For curl
if [[ -d /usr/local/opt/curl ]]; then
  export PATH="/usr/local/opt/curl/bin:$PATH"
fi

# Do not register duplicate paths
typeset -U path cdpath fpath manpath

# for go
export GOPATH="$HOME/dev"
export PATH="$PATH:$GOPATH/bin"
export GO111MODULE=on


