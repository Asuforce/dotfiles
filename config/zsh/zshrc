################################################################################
# Initialization & Performance
################################################################################

# Auto compile zshrc for faster startup
if [[ ~/.zshrc -nt ~/.zshrc.zwc ]]; then
  zcompile ~/.zshrc
fi


################################################################################
# Environment Variables
################################################################################

# XDG base directory specification
export XDG_CONFIG_HOME="$HOME/.config"

export EDITOR=nvim
export GOPATH="$HOME/dev"

# Homebrew PATH configuration (ARM64 and Intel support)
if [[ "$(uname -m)" == "arm64" ]]; then
  export PATH="/opt/homebrew/bin:$PATH"
  export PATH="/opt/homebrew/sbin:$PATH"
else
  export PATH="/usr/local/bin:$PATH"
  export PATH="/usr/local/sbin:$PATH"
fi

# Go binaries
export PATH="$PATH:$GOPATH/bin"

# Curl binary path
if [[ -d /usr/local/opt/curl ]]; then
  export PATH="/usr/local/opt/curl/bin:$PATH"
fi

# Deduplicating path entries
typeset -U path cdpath fpath manpath


################################################################################
# Plugin Configuration
################################################################################

# Load sheldon plugin manager
eval "$(sheldon source)"

# Zoxide initialization (smart directory jumper)
eval "$(zoxide init zsh)"

# Color support
autoload -Uz colors

# Mise language runtime manager (lazy loaded)
eval "$(mise activate zsh --shims)"


################################################################################
# History Configuration
################################################################################

HISTFILE=~/.zsh_history
HISTSIZE=1000000
SAVEHIST=1000000

setopt HIST_IGNORE_DUPS        # Ignore duplicate commands
setopt HIST_IGNORE_ALL_DUPS    # Remove duplicates from history
setopt HIST_IGNORE_SPACE       # Don't record commands starting with space
setopt HIST_FIND_NO_DUPS       # Don't display duplicates when searching
setopt HIST_REDUCE_BLANKS      # Remove blank lines from history
setopt HIST_NO_STORE           # Don't store history command in history
setopt SHARE_HISTORY           # Share history among shell sessions


################################################################################
# Completion Configuration
################################################################################

# Case-insensitive and substring completion
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# Ignore parent directories in completion
zstyle ':completion:*' ignore-parents parent pwd ..

# Process name completion for ps command
zstyle ':completion:*:processes' command 'ps x -o pid,s,args'

# Word delimiter configuration
autoload -Uz select-word-style
select-word-style default


################################################################################
# Zsh Options
################################################################################

# Display Japanese filenames correctly
setopt print_eight_bit

# Disable beep on errors
setopt no_beep

# Disable flow control (Ctrl+S/Ctrl+Q)
setopt no_flow_control

# Do not exit on Ctrl+D
setopt ignore_eof

# Treat text after '#' as comments
setopt interactive_comments

# Change directory without 'cd' command
setopt auto_cd

# Automatically push directory on 'cd'
setopt auto_pushd

# Don't add duplicate directories to stack
setopt pushd_ignore_dups

# Enable extended glob patterns
setopt extended_glob


################################################################################
# Keybindings
################################################################################

# Emacs keybindings
bindkey -e

# Incremental history search
bindkey '^R' history-incremental-pattern-search-backward


################################################################################
# Notification System
################################################################################

# Threshold for notifications (in seconds)
export AUTO_NOTIFY_THRESHOLD=5

# Commands to exclude from notifications
export AUTO_NOTIFY_IGNORE=("lazygit" "lazydocker" "btop" "viddy" "cat" "nvim" "vi")

# Notification format
export AUTO_NOTIFY_TITLE="Command finished"
export AUTO_NOTIFY_BODY="'%command' completed in %elapsed seconds"


################################################################################
# FZF Integration Functions
################################################################################

# Search zoxide directory history with fzf
# Keybinding: Ctrl+v
fzf-z-search() {
  local res=$(zoxide query -l | sort -rn | fzf)
  if [[ -n "$res" ]]; then
    BUFFER+="cd $res"
    zle accept-line
  else
    return 1
  fi
}
zle -N fzf-z-search
bindkey '^v' fzf-z-search

# Search ghq repositories with fzf
# Keybinding: Ctrl+j
fzf-src() {
  local selected_dir=$(ghq list -p | fzf --query "$LBUFFER")
  if [[ -n "$selected_dir" ]]; then
    BUFFER="cd ${selected_dir}"
    zle accept-line
  fi
}
zle -N fzf-src
bindkey '^j' fzf-src

# Search command history with fzf
# Keybinding: Ctrl+]
fzf-history() {
  BUFFER=$(fc -l -r 1 | awk '{$1=""; print substr($0,2)}' | uniq | fzf)
  CURSOR=$#BUFFER
}
zle -N fzf-history
bindkey '^]' fzf-history

# Search and checkout git branches with fzf
# Keybinding: Ctrl+[
git-branch-fzf() {
  local selected_branch=$(git for-each-ref --format='%(refname)' --sort=-committerdate refs/heads | perl -pne 's{^refs/heads/}{}' | fzf --query "$LBUFFER")

  if [[ -n "$selected_branch" ]]; then
    BUFFER="git checkout ${selected_branch}"
    zle accept-line
  fi

  zle reset-prompt
}

zle -N git-branch-fzf
bindkey "^[" git-branch-fzf

# Search and open files with tree and fzf
# Keybinding: Ctrl+t
tree-fzf() {
  local SELECTED_FILE=$(tree --charset=o -f | fzf --query "$LBUFFER" | tr -d '\||`|-' | xargs echo)

  if [[ -n "$SELECTED_FILE" ]]; then
    BUFFER="$EDITOR $SELECTED_FILE"
    zle accept-line
  fi

  zle reset-prompt
}

zle -N tree-fzf
bindkey "^t" tree-fzf


################################################################################
# Aliases (Categorized)
################################################################################

# Manage and display aliases by category
aliases() {
  local file="$HOME/.zshrc"
  if [[ -z "${1:-}" ]]; then
    # Display all aliases with category
    grep -B 1 "^alias [a-z]" "$file" | grep -v "^--$" | awk '/^# @/{tag=$0; next} {print tag " " $0}' | sed 's/# @//' | column -t
  else
    # Display aliases for specific category
    grep -B 1 "^alias [a-z]" "$file" | grep -v "^--$" | awk "/^# @$1/{tag=1; next} /^# @/{tag=0} tag && /^alias/{print}" | column -t
  fi
}

# System utilities
# @system
alias cp='cp -i'
# @system
alias mv='mv -i'
# @system
alias mkdir='mkdir -p'

# File listing with eza
# @system
alias ls='eza -GF'
# @system
alias ll='eza -l'
# @system
alias la='eza -alh'

# File viewing with bat
# @system
alias cat='bat'

# Editor
# @system
alias vi='nvim'

# Git
# @git
alias g='git'

# Homebrew
# @brew
alias bu='env HOMEBREW_INSTALL_CLEANUP=1 brew upgrade --fetch-HEAD --display-times && brew upgrade --cask'

# Docker
# @docker
alias de='docker exec'
# @docker
alias d-c='docker-compose'

# Kubernetes
# @kubectl
alias k='kubectl'
# @kubectl
alias kg='kubectl get'
# @kubectl
alias kc='kubectx'
# @kubectl
alias kn='kubens'
# @kubectl
alias kv='k9s'

# Lazy*
# @lazygit
alias lg='lazygit'
# @lazydocker
alias ld='lazydocker'

# Shell management
# @system
alias re='exec $SHELL -l'

# VSCode
# @system
alias e='code -r'

# GNU sed on macOS
if [[ "$(uname)" == "Darwin" ]]; then
  # @system
  alias sed='gsed'
fi
